#include "imports/stdlib.fc";

builder store_int_address256(builder b, int address) {
    return b.store_uint(4, 3)
    .store_uint(0, 8)
    .store_uint(address, 256);
}

{-
    admin_address: MsgAddressInt
    users: (HashmapE 256 uint32)
    total: uint32
-}
(slice, cell) load_data() impure inline_ref {
    var ds = get_data().begin_parse();
    return (ds~load_msg_addr(), ds~load_dict());
}

() save_data(slice admin_address, cell user_shares) impure inline_ref {
    set_data(
        begin_cell()
        .store_slice(admin_address)
        .store_dict(user_shares)
        .end_cell()
    );
}

(int) get_total_shares(cell user_shares) impure inline_ref {
    var (int address, slice shares, int success?) = udict_get_min?(user_shares, 256);
    int total = 0;
    while (success?) {
        total += shares~load_uint(32);
        (address, shares, success?) = udict_get_next?(user_shares, 256, address);
    }

    return total;
}

;; shares success?
(int, int) _get_shares_by_address(slice address) impure inline_ref {
    var (_, user_shares) = load_data();
    address~skip_bits(11);
    int index = address~load_uint(256);
    var (d, success?) = user_shares.udict_get?(256, index);
    if (success?) {
        return (d~load_uint(32), -1);
    }

    return (0, 0);
}

(int) handler(cell in_msg_cell, slice in_msg_body) impure inline_ref {
    int op = 0;
    if (slice_bits(in_msg_body) >= 32) {
        op = in_msg_body~load_uint(32);
    }

    var sc = in_msg_cell.begin_parse();
    ;; remove flags ???
    sc~load_uint(4);

    ;; load sender
    var sender = sc~load_msg_addr();

    {- storage$_ public_key:uint256 execution_time:uint32 receiver:MsgAddressInt seqno:uint32 = Storage; -}
    var (admin_address, user_shares) = load_data();

    {- Add user -}
    if (op == 0x368ddef3) {
        if (~ equal_slices(admin_address, sender)) {
            return 120;
        }

        {- msg body -}
        {- add_user#368ddef3 query_id:uint64 address:MsgAddressInt share:uint32 = InternalMsgBody; -}
        var (_, address, new_shares) = (
            in_msg_body~load_uint(64),
            in_msg_body~load_msg_addr(),
            in_msg_body~load_uint(32)
        );

        var (existing_shares, success?) = _get_shares_by_address(address);

        address~skip_bits(11);
        int address_hash = address~load_uint(256);

        if (success?) {
            user_shares~udict_delete_get?(256, address_hash);
        }
        user_shares~udict_set_builder(256, address_hash, begin_cell().store_uint(new_shares, 32));
        save_data(admin_address, user_shares);

        return 0;
    }

    {- Remove user -}
    if (op == 0x278205c8) {
        if (~ equal_slices(admin_address, sender)) {
            return 120;
        }

        {- remove_user#278205c8 query_id:uint64 address:MsgAddressInt = InternalMsgBody; -}
        var (_, address) = (
            in_msg_body~load_uint(64),
            in_msg_body~load_msg_addr()
        );

        var (existing_shares, success?) = _get_shares_by_address(address);

        address~skip_bits(11);
        int address_hash = address~load_uint(256);

        if (success?) {
            user_shares~udict_delete_get?(256, address_hash);
            save_data(admin_address, user_shares);
            return 0;
        }

        return 121;
    }

    {- Split TON -}
    if (op == 0x068530b3) {
        var (dict_key, dict_val, success?) = udict_get_min?(user_shares, 256);
        if (~ success?) {
            return 122;
        }

        {- split_ton#068530b3 query_id:uint64 = InternalMsgBody; -}
        var (query_id) = (in_msg_body~load_uint(64));

        ;; убираем из сообщение получателя (это адрес текущего контракта в данном случае)
        sc~load_msg_addr();
        int amount = sc~load_coins();

        int total_shares = user_shares.get_total_shares();
        while (success?) {
            int ton_amount = dict_val.preload_uint(32) * amount / total_shares;
            if (ton_amount) {
                var msg = begin_cell()
                .store_uint(0x18, 6)
                .store_int_address256(dict_key)
                .store_coins(ton_amount)
                .store_uint(0, 1 + 4 + 4)
                .store_uint(query_id, 64)
                .store_uint(0, 32 + 1 + 1)
                .end_cell();

                send_raw_message(msg, 1);
            }

            (dict_key, dict_val, success?) = udict_get_next?(user_shares, 256, dict_key);
        }

        return 0;
    }

    {- Transfer Notification (Jetton). -}
    if (op == 0x7362d09c) {
        var (dict_key, dict_val, success?) = udict_get_min?(user_shares, 256);
        if (~ success?) {
            return 122;
        }

        {- transfer_notification#7362d09c query_id:uint64 amount:Coins = InternalMsgBody; -}
        var (query_id, amount) = (in_msg_body~load_uint(64), in_msg_body~load_coins());

        int total_shares = user_shares.get_total_shares();
        while (success?) {
            int send_amount = dict_val.preload_uint(32) * amount / total_shares;
            if (send_amount) {
                ;; transfer#0f8a7ea5 query_id:uint64 amount:(VarUInteger 16) destination:MsgAddress
                ;;                   response_destination:MsgAddress custom_payload:(Maybe ^Cell)
                ;;                   forward_ton_amount:(VarUInteger 16) forward_payload:(Either Cell ^Cell)
                ;;                   = InternalMsgBody;
                var body = begin_cell()
                        .store_uint(0xf8a7ea5, 32)
                        .store_uint(query_id, 64)
                        .store_coins(send_amount)
                        .store_int_address256(dict_key)
                        .store_int_address256(dict_key)
                        .store_int(false, 1)
                        .store_coins(1)
                        .store_int(false, 1)
                        .end_cell();

                var msg = begin_cell()
                .store_uint(0x18, 6)
                .store_slice(my_address())
                .store_coins(20000000)
                .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1)
                .store_uint(1, 1)
                .store_ref(body)
                .end_cell();

                send_raw_message(msg, 1);
            }

            (dict_key, dict_val, success?) = udict_get_next?(user_shares, 256, dict_key);
        }

        return 0;
    }

    return 0;
}

() recv_internal(cell in_msg_cell, slice in_msg_body) impure {
    int handled = handler(in_msg_cell, in_msg_body);

    if (handled) {
        throw(handled);
    }
}

(int) get_prefix() impure method_id {
    var (admin_address, user_shares) = load_data();
;;
;;;;    var prefix = admin_address~load_msg_addr();
;;    return admin_address~load_uint(11);

    return user_shares.get_total_shares();
}

(slice) get_admin_address() impure method_id {
    var (admin_address, _) = load_data();
    return admin_address;
}

(cell) get_users() impure method_id {
    var (_, user_shares) = load_data();
    return user_shares;
}

(int) get_user_share(slice address) impure method_id {
    var (int shares, _) = _get_shares_by_address(address);
    return shares;
}
